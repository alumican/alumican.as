/**
 * Progression 4
 * 
 * @author Copyright (C) 2007-2009 taka:nium.jp, All Rights Reserved.
 * @version 4.0.1 Public Beta 1.1
 * @see http://progression.jp/
 * 
 * Progression Software is released under the Progression Software License:
 * http://progression.jp/ja/overview/license
 * 
 * Progression Libraries is released under the MIT License:
 * http://www.opensource.org/licenses/mit-license.php
 */
package net.alumican.as3.framework.progression4.commands
{
	import flash.display.DisplayObject;
	import flash.events.Event;
	import flash.events.IEventDispatcher;
	import jp.progression.casts.getInstanceById;
	import jp.progression.commands.Func;
	import jp.progression.commands.Command;
	import jp.nium.utils.ObjectUtil;
	import net.alumican.as3.framework.progression4.events.ActionEvent;
	
	/**
	 * オブジェクトのカスタムアクションを実行するコマンド
	 */
	public class DoAction extends Command
	{
		//----------------------------------------
		//CLASS CONSTANTS
		
		
		
		
		
		//----------------------------------------
		//VARIABLES
		
		/**
		 * ターゲット
		 */
		public function get target():* { return _target; }
		public function set target(value:*):void { _target = value; }
		private var _target:*;
		
		/**
		 * 実行引数
		 */
		public function get args():Array { return _args; }
		public function set args(value:Array):void { _args = value; }
		private var _args:Array;
		
		/**
		 * 実行アクション
		 */
		public function get action():String { return _action; }
		public function set action(value:String):void { _action = value; }
		private var _action:String;
		
		/**
		 * 内部実行コマンド
		 */
		private var _command:Func;
		
		
		
		
		
		//----------------------------------------
		//METHODS
		
		/**
		 * コンストラクタ
		 */
		public function DoAction(target:*, action:String, args:Array = null, initObject:Object = null):void
		{
			_target = target;
			_action = action;
			_args = args;
			
			//親クラスを初期化する
			super(_executeFunction, _interruptFunction, initObject);
		}
		
		/**
		 * コマンド実行ハンドラ
		 */
		private function _executeFunction():void
		{
			//ターゲットインスタンスから実行関数を取得
			var instance:* = (_target is String) ? getInstanceById(_target) : _target;
			var func:Function = instance["doAction"];
			_command = new Func(func, [_action, _args], instance, ActionEvent.COMPLETE);
			_command.onComplete = _executeComplete;
			
			//コマンドの実行
			_command.execute();
		}
		
		/**
		 * コマンド中断ハンドラ
		 */
		private function _interruptFunction():void
		{
			_command.interrupt();
		}
		
		/**
		 * コマンド完了ハンドラ
		 */
		private function _executeComplete():void
		{
			if (super.state > 1)
			{
				super.executeComplete();
			}
		}
		
		/**
		 * クローンを取得する
		 * @return
		 */
		public override function clone():Command
		{
			return new DoAction(_target, _action, _args, this);
		}
		
		/**
		 * ストリング表現を取得する
		 * @return
		 */
		public override function toString():String
		{
			return ObjectUtil.formatToString(this, super.className, super.id ? "id" : null, "target", "action", "args");
		}
	}
}
